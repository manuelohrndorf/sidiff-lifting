grammar org.eclipse.emf.refactor.examples.SimpleWebModelingLanguage with org.eclipse.xtext.common.Terminals

generate simpleWebModelingLanguage "http://www.eclipse.org/emf/refactor/examples/SimpleWebModelingLanguage"


WebModel:
	'webmodel' name=ID '{'
		dataLayer=DataLayer
		hypertextLayer=HypertextLayer
	'}' ;

DataLayer:
	'data {' {DataLayer}
		entities+=Entity*
	'}' ;

Entity:
	'entity' name=ID '{'
		attributes+=Attribute*
		references+=Reference*
	'}' ;

Attribute:
	'att' name=ID ':' type=SimpleType
;

enum SimpleType:
	Boolean | Email | Float | Integer | String
;

Reference:
	'ref' name=ID ':' type=[Entity]
;

HypertextLayer:
	'hypertext {' 
		pages+=Page+
		'start page is' startPage=[StaticPage]
	'}' ;

Page:
	StaticPage | DynamicPage
;

StaticPage:
	'static page' name=ID '{'
	links+=Link*
	'}' ;

Link:
	'link to page' target=[Page] 
;

DynamicPage:
	IndexPage | DataPage
;

IndexPage:
	'index page' name=ID ('shows entity' entity=[Entity])?  '{'
	links+=Link*
	'}' ;

DataPage:
	'data page' name=ID ('shows entity' entity=[Entity])?  '{'
	links+=Link*
	'}' ;
